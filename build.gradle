import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.3.10'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.3.10'
    id 'org.springframework.boot' version '3.5.11'
    id 'jacoco'
}

group = 'com.store'
version = '1.0'
def springBootVersion = '3.5.11'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
    implementation('jakarta.validation:jakarta.validation-api:3.1.1')
    implementation("org.springframework.boot:spring-boot-starter:${springBootVersion}") {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-validation:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-oauth2-client:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-oauth2-resource-server:${springBootVersion}")
    implementation('com.nimbusds:nimbus-jose-jwt:10.7')
    implementation('org.jetbrains.kotlin:kotlin-reflect')
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')

    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testImplementation('io.specmatic:junit5-support:2.39.6')
    testImplementation('org.jetbrains.kotlin:kotlin-test-junit5')
    testImplementation('org.testcontainers:junit-jupiter:1.21.4')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '17'
    }
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
            exclude '**/*.kt'
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jvmArgs '-Dfile.encoding=UTF-8'
}

jacoco {
    toolVersion = '0.8.14'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/DB.*',
                    '**/APIKeyAuthFilter.*'
            ])
        }))
    }
}
